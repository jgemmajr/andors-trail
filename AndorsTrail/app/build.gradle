apply plugin: 'com.android.application'

android {

    compileSdkVersion 34

    defaultConfig {
        applicationId "com.gpl.rpg.AndorsTrail"
        minSdkVersion 14
        targetSdkVersion 34
    }

    buildTypes {
        release {
            manifestPlaceholders icon_name: 'icon', fileproviderPath: 'AndorsTrail'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            manifestPlaceholders icon_name: 'icon_beta', fileproviderPath: 'AndorsTrail.beta2'
            applicationIdSuffix 'beta2'
			signingConfig signingConfigs.debug
        }
    }
    namespace 'com.gpl.rpg.AndorsTrail'
    
    sourceSets {
        main {
            res.srcDirs = ['build/gen-res', 'src/main/res']
            assets.srcDirs = ['build/gen-assets', 'src/main/assets']
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}

task copyRes(type: Copy) {
    description "Copies the res folder to the modules res folder (& renames .tmx to .xml)"
    from "${rootDir}/res"
    into "${projectDir}/build/gen-res"
    rename "(.*)\\.tmx", "\$1.xml"
}

task copyResValuesIn(type: Copy) {
    description "Exception Indonesian language code (Old IN was renamed to now ID)"
    from "${rootDir}/res/values-id/"
    into "${projectDir}/build/gen-res/values-in/"
}

task copyTranslation(type: Copy) {
    description("Copies the translation files to the modules translations folder")
    from "${rootDir}/assets/translation"
    into "${projectDir}/build/gen-assets/translation"
}

task cleanup(type: Delete) {
    description("Deletes the assets/translation and the res folder from the modules folder")
    delete "${projectDir}/build/gen-res", "${projectDir}/build/gen-assets/translation"
}

afterEvaluate {
    project.tasks.copyRes.dependsOn project.tasks.copyResValuesIn

    mapReleaseSourceSetPaths.dependsOn project.tasks.copyRes
    generateReleaseResources.dependsOn project.tasks.copyRes

    generateDebugResources.dependsOn project.tasks.copyRes
    mapDebugSourceSetPaths.dependsOn project.tasks.copyRes
    mergeDebugResources.dependsOn project.tasks.copyRes
    extractDeepLinksDebug.dependsOn project.tasks.copyRes
    mergeReleaseResources.dependsOn project.tasks.copyRes
    extractDeepLinksRelease.dependsOn project.tasks.copyRes

    generateDebugResources.dependsOn project.tasks.copyTranslation
    mapDebugSourceSetPaths.dependsOn project.tasks.copyTranslation
    mergeDebugAssets.dependsOn project.tasks.copyTranslation
    mergeReleaseAssets.dependsOn project.tasks.copyTranslation
    extractDeepLinksDebug.dependsOn project.tasks.copyTranslation
    extractDeepLinksRelease.dependsOn project.tasks.copyTranslation

    clean.dependsOn project.tasks.cleanup
}
